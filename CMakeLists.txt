PROJECT(EdelApp CXX)
CMAKE_MINIMUM_REQUIRED(VERSION 2.8.8)

# set project information
SET(PROJECT_VERSION_MAJOR "0")
SET(PROJECT_VERSION_MINOR "1")
SET(PROJECT_VERSION_PATCH "0")
SET(PROJECT_VERSION "${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}.${PROJECT_VERSION_PATCH}")
SET(PROJECT_VENDOR "Edeltech")
SET(PROJECT_COPYRIGHT_YEAR "2013")
SET(PROJECT_DOMAIN "edeltech.ch")

# define useful variables
STRING(TOLOWER ${PROJECT_NAME} PROJECT_NAME_LOWERCASE)
SET(EDEL_LIBRARIES_DIR "${CMAKE_SOURCE_DIR}/src/libs")
SET(EDEL_GFX_DIR "${CMAKE_SOURCE_DIR}/gfx")

# define a macro to get a list of subdirectory
MACRO(SUBDIRLIST result curdir)
  FILE(GLOB children RELATIVE ${curdir} ${curdir}/*)
  SET(dirlist "")
  FOREACH(child ${children})
    IF(IS_DIRECTORY ${curdir}/${child})
        SET(dirlist ${dirlist} ${child})
    ENDIF()
  ENDFOREACH()
  SET(${result} ${dirlist})
ENDMACRO()

# set the output paths
SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/bin")
SET(CMAKE_ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/lib")
SET(CMAKE_LIBRARY_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/lib")

# set build type to Debug is not specified
IF(NOT CMAKE_BUILD_TYPE)
    SET(CMAKE_BUILD_TYPE "Debug" CACHE STRING "" FORCE)
ENDIF()

# find QtCore
FIND_PACKAGE(Qt4 4.8.0 COMPONENTS QtMain QtCore QtGui REQUIRED)
INCLUDE(UseQt4)

MESSAGE(STATUS "Building ${PROJECT_NAME} in ${CMAKE_BUILD_TYPE} mode")

# enable automatic handling of moc files
SET(CMAKE_AUTOMOC TRUE)

# enable unit tests
ENABLE_TESTING()

# emulate 'make check'
ADD_CUSTOM_TARGET(check COMMAND ${CMAKE_CTEST_COMMAND})

# automatically include the source and build directories in the include path
SET(CMAKE_INCLUDE_CURRENT_DIR ON)

# include source or build tree headers before any other installed headers
SET(CMAKE_INCLUDE_DIRECTORIES_PROJECT_BEFORE ON)

# enable all compiler warnings on GCC and Apple
IF(CMAKE_COMPILER_IS_GNUCXX OR CMAKE_CXX_COMPILER MATCHES "clang")
	ADD_DEFINITIONS(-Wall -Wextra -fvisibility=hidden)
ENDIF()

# define project version (NOTE: replace with config.h.cmake ?)
ADD_DEFINITIONS(-DPROJECT_VERSION=\"${PROJECT_VERSION}\")

# define install directories
IF(UNIX AND NOT APPLE)
	SET(BIN_INSTALL_DIR "bin")
	SET(DOC_INSTALL_DIR "share/doc/${PROJECT_NAME_LOWERCASE}/")
ELSE()
	SET(BIN_INSTALL_DIR ".")
	SET(DOC_INSTALL_DIR ".")
ENDIF()

# add src files directory
ADD_SUBDIRECTORY(src)

# packaging using cpack
IF(APPLE)
	SET(CMAKE_INSTALL_PREFIX "/Applications")
ENDIF()

MESSAGE(STATUS "${PROJECT_NAME} will be installed to ${CMAKE_INSTALL_PREFIX}")

# install license and readme files
SET(LICENSE_FILE "LICENSE")
SET(README_FILE "README.md")
IF(NOT APPLE)
	INSTALL(FILES "${LICENSE_FILE}" "${README_FILE}" DESTINATION ${DOC_INSTALL_DIR})
ENDIF()

# define CPack variables (NOTE: Do this is a separte .cmake file)
SET(CPACK_GENERATOR "TBZ2")
SET(CPACK_PACKAGE_VERSION_MAJOR "${PROJECT_VERSION_MAJOR}")
SET(CPACK_PACKAGE_VERSION_MINOR "${PROJECT_VERSION_MINOR}")
SET(CPACK_PACKAGE_VERSION_PATCH "${PROJECT_VERSION_PATCH}")
SET(CPACK_PACKAGE_VERSION "${PROJECT_VERSION}")
SET(CPACK_PACKAGE_VENDOR "${PROJECT_VENDOR}")
SET(CPACK_RESOURCE_FILE_README "${CMAKE_SOURCE_DIR}/${README_FILE}")
if(WIN32)
    SET(CPACK_GENERATOR "NSIS")
    SET(CPACK_PACKAGE_EXECUTABLES "${PROJECT_NAME}" "${PROJECT_NAME}")
    SET(CPACK_PACKAGE_INSTALL_DIRECTORY "${PROJECT_NAME}")
    SET(CPACK_PACKAGE_FILE_NAME "${PROJECT_NAME} ${PROJECT_VERSION}")
    SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_SOURCE_DIR}/${LICENSE_FILE}")
    SET(CPACK_NSIS_EXECUTABLES_DIRECTORY "${BIN_INSTALL_DIR}")
    #SET(CPACK_NSIS_MUI_ICON "${PROJECT_ICONS_DIRECTORY}/NSIS.ico")
    #SET(CPACK_PACKAGE_ICON "${PROJECT_ICONS_DIRECTORY}\\\\NSISHeader.bmp")
    SET(CPACK_NSIS_URL_INFO_ABOUT "http://${PROJECT_DOMAIN}")
    SET(CPACK_NSIS_INSTALLED_ICON_NAME "${PROJECT_NAME}${CMAKE_EXECUTABLE_SUFFIX}")
    SET(CPACK_NSIS_MENU_LINKS "${LICENSE_FILE}" "License" "${README_FILE}" "Readme")
    SET(CPACK_NSIS_MUI_FINISHPAGE_RUN "${CPACK_NSIS_INSTALLED_ICON_NAME}")
ELSEIF(APPLE)
     SET(CPACK_GENERATOR "DragNDrop")
     SET(CPACK_DMG_FORMAT "UDBZ")
     SET(CPACK_DMG_VOLUME_NAME "${PROJECT_NAME}")
     SET(CPACK_PACKAGE_FILE_NAME "${PROJECT_NAME}-${PROJECT_VERSION}")
     #SET(CPACK_PACKAGE_ICON "${ICONS_DIR}/DMG.icns")
     #SET(CPACK_DMG_DS_STORE "${ICONS_DIR}/DMGDSStore")
     #SET(CPACK_DMG_BACKGROUND_IMAGE "${ICONS_DIR}/DMGBackground.png")
ELSEIF(UNIX)
    SET(CPACK_SYSTEM_NAME "${CMAKE_SYSTEM_NAME}-${CMAKE_SYSTEM_PROCESSOR}")
ENDIF()

INCLUDE(CPack)

# install MSVC redistributables and files listed in CMAKE_INSTALL_DEBUG_LIBRARIES
SET(CMAKE_INSTALL_SYSTEM_RUNTIME_DESTINATION "${BIN_INSTALL_DIR}")
INCLUDE(InstallRequiredSystemLibraries)

