# find all source files
FILE(GLOB_RECURSE APP_SRCS *.cpp)

# add resource file
QT4_ADD_RESOURCES(APP_RCCS "${PROJECT_NAME_LOWERCASE}.qrc")

# add a resource file for windows
IF(MSVC)
	LIST(APPEND APP_RCCS "${PROJECT_NAME_LOWERCASE}.rc")
ENDIF()

# set OS X bundle informations and copy app icon to bundle
IF(APPLE)
	SET(MACOSX_BUNDLE_INFO_STRING "${PROJECT_NAME} ${PROJECT_VERSION}")
	SET(MACOSX_BUNDLE_ICON_FILE "${PROJECT_NAME_LOWERCASE}.icns")
	SET(MACOSX_BUNDLE_GUI_IDENTIFIER "${PROJECT_DOMAIN_SECOND}.${PROJECT_DOMAIN_FIRST}")
	SET(MACOSX_BUNDLE_LONG_VERSION_STRING "${PROJECT_NAME} ${PROJECT_VERSION}")
	SET(MACOSX_BUNDLE_BUNDLE_NAME "${PROJECT_NAME}")
	SET(MACOSX_BUNDLE_SHORT_VERSION_STRING "${PROJECT_VERSION}")
	SET(MACOSX_BUNDLE_BUNDLE_VERSION "${PROJECT_NAME} ${PROJECT_VERSION}")
	SET(MACOSX_BUNDLE_COPYRIGHT "${PROJECT_COPYRIGHT_YEAR} ${PROJECT_VENDOR}")

	SET(BUNDLE_RESOURCES "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${PROJECT_NAME}.app/Contents/Resources")
	SET(BUNDLE_ICON "${CMAKE_CURRENT_SOURCE_DIR}/${MACOSX_BUNDLE_ICON_FILE}")
	EXECUTE_PROCESS(COMMAND ${CMAKE_COMMAND} -E make_directory ${BUNDLE_RESOURCES})
	EXECUTE_PROCESS(COMMAND ${CMAKE_COMMAND} -E copy_if_different ${BUNDLE_ICON} ${BUNDLE_RESOURCES})
ENDIF()

# add app executable
ADD_EXECUTABLE(${PROJECT_NAME} MACOSX_BUNDLE ${APP_SRCS})

MESSAGE(STATUS "Link target ${CMAKE_PROJECT_NAME} against ${QT_LIBRARIES} and ${EDEL_LIBRARIES}")

TARGET_LINK_LIBRARIES(${CMAKE_PROJECT_NAME} ${QT_LIBRARIES} ${EDEL_LIBRARIES})

# define app installation paths
INSTALL(TARGETS ${PROJECT_NAME} 
    BUNDLE DESTINATION ${BIN_INSTALL_DIR}
    RUNTIME DESTINATION ${BIN_INSTALL_DIR})

# deploy Qt4 app (install and setup all the linked dependencies)
INCLUDE(DeployQt4)

IF(APPLE)
    SET(EXECUTABLE_NAME "${PROJECT_NAME}.app")
ELSEIF(WIN32)
    SET(EXECUTABLE_NAME "${PROJECT_NAME}${CMAKE_EXECUTABLE_SUFFIX}")
ELSE()
    SET(EXECUTABLE_NAME "${BIN_INSTALL_DIR}/${PROJECT_NAME}${CMAKE_EXECUTABLE_SUFFIX}")
ENDIF()
    
INSTALL_QT4_EXECUTABLE("${EXECUTABLE_NAME}")